apply plugin: 'com.android.application'

def getKey(Properties properties, String name, String defaultValue) {
    return properties.containsKey(name) ? properties.getProperty(name) : defaultValue
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "com.tlongdev.bktf"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 51
        versionName "2.2.3"

        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                outputFileName  = "bptf-" + versionName + ".apk"
            }
        }

        Properties keys = new Properties()
        if (project.rootProject.file('keys.properties').exists()) {
            keys.load(project.rootProject.file('keys.properties').newDataInputStream())
        }

        buildConfigField("String", "BACKPACK_TF_API_KEY", "\"${getKey(keys, "BACKPACK_TF_API_KEY", "59726b45cf6c756db606045b")}\"")
        buildConfigField("String", "STEAM_WEB_API_KEY", "\"${getKey(keys, "STEAM_WEB_API_KEY", "28FDEDBC9C629D932EC7E0AE4A25AC6A")}\"")
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard_general.pro', 'disable_logs.pro'
        }
        debug {
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    def support_version = '27.1.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs', excludes: ['*sources.jar'])
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation "org.robolectric:robolectric:3.1"

    annotationProcessor 'com.jakewharton:butterknife-compiler:8.1.0'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.5'
    annotationProcessor 'com.f2prateek.dart:dart-processor:2.0.0'
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
    implementation('com.squareup.okhttp3:okhttp:3.3.1') {
        exclude group: 'com.squareup.okio', module: 'okio'
    }
    implementation "com.android.support:support-annotations:$support_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:customtabs:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:percent:$support_version"
    implementation 'com.f2prateek.dart:dart:2.0.0'
    implementation 'com.google.dagger:dagger:2.5'
    implementation 'com.google.firebase:firebase-messaging:17.0.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v2.2.5'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.jakewharton:butterknife:8.1.0'
    implementation 'com.squareup.retrofit2:retrofit:2.1.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.1.0'
}

apply plugin: 'com.google.gms.google-services'