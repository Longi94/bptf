apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'

def getKey(Properties properties, String name, String defaultValue) {
    return properties.containsKey(name) ? properties.getProperty(name) : defaultValue
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId "com.tlongdev.bktf"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 47
        versionName "2.1.5"

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                output.outputFile = new File((String) output.outputFile.parent, "bptf-" + versionName + ".apk")
            }
        }

        Properties keys = new Properties()
        if (project.rootProject.file('keys.properties').exists()) {
            keys.load(project.rootProject.file('keys.properties').newDataInputStream())
        }

        buildConfigField("String", "BACKPACK_TF_API_KEY", "\"${getKey(keys, "BACKPACK_TF_API_KEY", "DUMMY_KEY")}\"")
        buildConfigField("String", "STEAM_WEB_API_KEY", "\"${getKey(keys, "STEAM_WEB_API_KEY", "DUMMY_KEY")}\"")
        resValue("string", "BANNER_AD_UNIT_ID", "${getKey(keys, "BANNER_AD_UNIT_ID", "ca-app-pub-3940256099942544/6300978111")}")
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard_general.pro', 'disable_logs.pro'
        }
        debug {
            resValue("string", "BANNER_AD_UNIT_ID", "ca-app-pub-3940256099942544/6300978111")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs', excludes: ['*sources.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.robolectric:robolectric:3.1-SNAPSHOT"

    apt 'com.jakewharton:butterknife-compiler:8.0.0'
    apt 'com.google.dagger:dagger-compiler:2.2'
    provided 'com.f2prateek.dart:dart-processor:2.0.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile('com.crashlytics.sdk.android:answers:1.3.6@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile('com.squareup.okhttp3:okhttp:3.2.0') {
        exclude group: 'com.squareup.okio', module: 'okio'
    }
    compile 'com.android.support:support-annotations:23.4.0'
    compile 'com.android.support:support-v4:23.4.0'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'
    compile 'com.android.support:customtabs:23.4.0'
    compile 'com.android.support:recyclerview-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:percent:23.4.0'
    compile 'com.f2prateek.dart:dart:2.0.0'
    compile 'com.google.android.gms:play-services-analytics:8.4.0'
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    compile 'com.google.android.gms:play-services-ads:8.4.0'
    compile 'com.google.dagger:dagger:2.2'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.4'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.jakewharton:butterknife:8.0.1'
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
}

apply plugin: 'com.google.gms.google-services'