apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'io.fabric'

def String getKey(Properties properties, String name, String defaultValue) {
    return properties.containsKey(name) ? properties.getProperty(name) : defaultValue
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {
        applicationId 'com.tlongdev.bktf'
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 50
        versionName '2.3.0'

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                output.outputFile = new File((String) output.outputFile.parent, 'bptf-' + versionName + '.apk')
            }
        }

        Properties keys = new Properties()
        if (project.rootProject.file('keys.properties').exists()) {
            keys.load(project.rootProject.file('keys.properties').newDataInputStream())
        }

        buildConfigField('String', 'BACKPACK_TF_API_KEY', '"' + getKey(keys, 'BACKPACK_TF_API_KEY', 'DUMMY_KEY') + '"')
        buildConfigField('String', 'STEAM_WEB_API_KEY', '"' + getKey(keys, 'STEAM_WEB_API_KEY', 'DUMMY_KEY') + '"')
        resValue('string', 'BANNER_AD_UNIT_ID', getKey(keys, 'BANNER_AD_UNIT_ID', 'ca-app-pub-3940256099942544/6300978111'))

        manifestPlaceholders = [
                fabricApiKey: getKey(keys, 'FABRIC_API_KEY', 'DUMMY_KEY')
        ]
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard_general.pro', 'disable_logs.pro'
            buildConfigField('String', 'TLONGDEV_API_VERSION', '"v1"')
        }
        debug {
            resValue('string', 'BANNER_AD_UNIT_ID', 'ca-app-pub-3940256099942544/6300978111')
            buildConfigField('String', 'TLONGDEV_API_VERSION', '"dev"')
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs', excludes: ['*sources.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile "org.robolectric:robolectric:3.1"

    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    apt 'com.google.dagger:dagger-compiler:2.8'
    provided 'com.f2prateek.dart:dart-processor:2.0.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile('com.crashlytics.sdk.android:answers:1.3.10@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:crashlytics:2.6.5@aar') {
        transitive = true;
    }
    compile('com.squareup.okhttp3:okhttp:3.5.0') {
        exclude group: 'com.squareup.okio', module: 'okio'
    }
    compile 'com.android.support:support-annotations:25.1.1'
    compile 'com.android.support:support-v4:25.1.1'
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:cardview-v7:25.1.1'
    compile 'com.android.support:customtabs:25.1.1'
    compile 'com.android.support:recyclerview-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    compile 'com.android.support:percent:25.1.1'
    compile 'com.f2prateek.dart:dart:2.0.1'
    compile 'com.google.android.gms:play-services-analytics:10.0.1'
    compile 'com.google.dagger:dagger:2.8'
    compile 'com.google.firebase:firebase-ads:10.0.1'
    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.google.firebase:firebase-messaging:10.0.1'
    compile 'com.github.PhilJay:MPAndroidChart:v2.2.5'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.jakewharton:butterknife:8.4.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
}

apply plugin: 'com.google.gms.google-services'